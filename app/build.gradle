plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id "dagger.hilt.android.plugin"
    id "kotlin-kapt"
}

android {
    namespace 'bruhcollective.itaysonlab.jetisteam'
    compileSdk 33

    defaultConfig {
        applicationId "bruhcollective.itaysonlab.jetisteam"
        minSdk 21
        targetSdk 33
        versionCode 4
        versionName "2.0 alpha 1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
}

androidComponents {
    onVariants(selector()) {
        packaging.resources.excludes.add("commonMain/**")
        packaging.resources.excludes.add("META-INF/**/previous-compilation-data.bin")
        packaging.resources.excludes.add("META-INF/*.json")
    }

    onVariants(selector().withBuildType("release")) {
        packaging.resources.excludes.add("META-INF/*.version")
        packaging.resources.excludes.add("META-INF/*.kotlin_module")
        packaging.resources.excludes.add("META-INF/**")
        packaging.resources.excludes.add("META-INF/{AL2.0,LGPL2.1}")
        packaging.resources.excludes.add("**/*.properties")
        packaging.resources.excludes.add("DebugProbesKt.bin")
        packaging.resources.excludes.add("kotlin-tooling-metadata.json")
        packaging.resources.excludes.add("log4j2.xml")
        packaging.resources.excludes.add("**.proto")
        packaging.resources.excludes.add("mozilla/public-suffix-list.txt")
    }
}

kapt {
    correctErrorTypes = true
}

kotlin {
    jvmToolchain(11)
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.2"

    implementation project(":domain")
    implementation project(":data")
    implementation project(":uikit")

    implementation project(":microapp:core")
    implementation project(":microapp:auth")

    implementation project(":microapp:notifications")
    implementation project(":microapp:guard_ks")
    implementation project(":microapp:library_ks")
    implementation project(":microapp:profile")

    /*implementation project(":microapp:home")

    implementation project(":microapp:news")
    implementation project(":microapp:gamepage")
    implementation project(":microapp:library")
    implementation project(":microapp:market")*/

    implementation 'com.github.penfeizhou.android.animation:apng:2.24.0'

    implementation libs.androidCoreSplashscreen

    implementation libs.bundles.androidx
    implementation libs.bundles.compose
    implementation libs.bundles.accompanist
    implementation libs.bundles.ksteam

    implementation libs.coilCompose
    implementation libs.coilGif

    implementation libs.hiltNavigation
    implementation libs.hiltAndroid
    kapt libs.hiltCompiler

    testImplementation libs.junit
    androidTestImplementation libs.androidTest

    debugImplementation libs.bundles.composeDebug
}